import com.ishland.c2me.gradle.RedirectingOutputStream

evaluationDependsOn(":tests")
evaluationDependsOn(":tests:test-mod")

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

loom {
    runs {
        testC2MEServer {
            server()
            runDir getRootProject().getProjectDir().toPath().relativize(project.getBuildDir().toPath()).resolve("testServer").toString()
            source sourceSets.testmod
            vmArgs "-Dc2me.base.config.override.ioSystem.replaceImpl=true",
                    "-Dc2me.base.config.override.asyncScheduling.enabled=true",
                    "--add-modules",
                    "jdk.incubator.vector"
            if (System.getenv("GITHUB_WORKFLOW") != null) {
                if (System.getenv("RUNNER_OS").equals("macOS")) {
                    vmArgs "-Xmx11G"
                } else if (System.getenv("RUNNER_OS").equals("Linux")) {
                    vmArgs "-Xmx5G"
                }
            }
        }
    }
}

afterEvaluate {
    runTestC2MEServer.configure {
        doFirst {
            standardOutput = new RedirectingOutputStream(project, getName())
        }
        onlyIf {
            !getRootProject().getProjectDir().toPath().relativize(project.getBuildDir().toPath()).resolve("testServer").toFile().isDirectory()
        }
    }

    migrateMappings.onlyIf { false }
}

dependencies {
    testmodImplementation project(":tests:test-mod").sourceSets.main.output
    testmodImplementation project(":").sourceSets.main.output
}

dependencies {
    afterEvaluate {
        (rootProject.subprojects - project(":tests")).forEach {
            if (it.parent != project(":")) return

            testmodImplementation project(path: ":${it.name}", configuration: "namedElements")
        }
    }
}

test {
    useJUnitPlatform()
}
